<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.0.18">
  <POU Name="FB_MOD" Id="{ce39c9c5-f1f6-48a4-885d-15a7f8be770f}">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_MOD EXTENDS FB_CtrlBase
VAR_INPUT
	///pointer to function block
	p_fbHOR: POINTER TO FB_CYL;
	///pointer to function block
	p_fbVER: POINTER TO FB_CYL;
END_VAR
VAR
	///Specific interface struct for public access
	SIf: T_Ctrl_SIf_MOD;
	pHOR_CIF: POINTER TO T_Ctrl_CIf;
	pVER_CIF: POINTER TO T_Ctrl_CIf;
	bolfpCmd: BOOL;
	
	// Data Channel To PLC  test
	intLastReadData : DINT;
	
	stDataChannelTest_DtChnToPlc : T_Data_DtChn;
	intStatemachine : INT;			// State of the Communication Statemachine
	bolDataBad: BOOL;				// Data not correct
	intCountRetry: INT;				// Retry Counts for error message
	intIndexOldest : INT;			// Temporary Index saved
	dtOldestTag : DATE_AND_TIME;	// Date and time of the oldes imput
	
	// local substates machine
	stSubSate: T_Ctrl_Class_StateMachine;
END_VAR
VAR CONSTANT
	// ctrl specific substates definitions
	cSUB_INITIALIZED: INT := 301;
	cSUB_VER_Up: INT := 302;
	cSUB_VER_Down: INT := 303;
	cSUB_WAIT_Pick: INT := 304;
	cSUB_HOR_MoveToPlace: INT := 305;
	cSUB_HOR_MoveToPick: INT := 306;
	cSUB_Finished: INT := 307;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*__________________________________________________________________________________________________
¦	
¦	Vistaprint Schweiz GmbH
¦	CH - 8401 Winterthur
¦
¦	www.vistaprint.ch - info@vistaprint.ch
¦___________________________________________________________________________________________________

Function desription:
This controller is an example of a level 2 modul controller. It has two cylinder FUB as childs. 
One is for up and down and the other is for a movement horizontal.(pick and place)


History:
Version		Date			Author		Comment
---------------------------------------------------------------------------------------------------
0.00.1		02.05.2013		AVME/KVO	Start history
0.00.2		03.07.2013		AVME/KVO	Always call Super.always

___________________________________________________________________________________________________*)

// check if child pointer valid
IF (me.bolActivate) AND (p_fbHOR<>0) AND (p_fbVER<>0) THEN // cyclic call enabled
	
	IF (CIf.Info.bolInitDone=FALSE) THEN // first cycle -> init
		CtrlInit();
		CIf.Info.bolInitDone := TRUE;
	ELSE
		CycleStartDuty();
	
		Always();
		
		StateMachine();
		
		CycleEndDuty();
	END_IF
	
END_IF;]]></ST>
    </Implementation>
    <Method Name="FB_init" Id="{554a885b-1a88-4e25-ad93-41fdf24e80f8}">
      <Declaration><![CDATA[METHOD FB_init : BOOL
VAR_INPUT
	///if TRUE, the retain variables are initialized (warm start / cold start)
	bInitRetains: BOOL;
	///if TRUE, the instance afterwards gets moved into the copy code (online change)
	bInCopyCode: BOOL;
END_VAR
VAR
	ii: UINT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//------------------------------------------------------------------------------------------------------------------------
// to manually change in new FUB implementation!!!
// define valid commands for this controller
FOR ii:= 0 TO (SIZEOF(CIf.Job.aCmdList)/SIZEOF(CIf.Job.aCmdList[0])-1) BY 1 DO
	IF (CIf.Job.aCmdList[ii].intCmd=cSTA_INITIALIZED) THEN
		// found first empty array element
		EXIT;
	END_IF
END_FOR

CIf.Job.aCmdList[ii].intCmd := cCMD_INIT;
CIf.Job.aCmdList[ii].strName := 'Init';
CIf.Job.aCmdList[ii].bolAvailable := TRUE;
ii := ii +1;
CIf.Job.aCmdList[ii].intCmd := cCMD_RUN;
CIf.Job.aCmdList[ii].strName := 'Run';
CIf.Job.aCmdList[ii].bolAvailable := TRUE;

// set name of class/typ
CIf.Info.strType := 'FB_MOD'; 

// set alarm class
SIf.Alm[1].eAlmClass := E_AlmClass.WARNING;
SIf.Alm[1].strText := 'not used';
SIf.Alm[2].eAlmClass := E_AlmClass.WARNING;
SIf.Alm[2].strText := 'not used';
SIf.Alm[3].eAlmClass := E_AlmClass.WARNING;
SIf.Alm[3].strText := 'not used';
SIf.Alm[4].eAlmClass := E_AlmClass.WARNING;
SIf.Alm[4].strText := 'not used';
SIf.Alm[5].eAlmClass := E_AlmClass.WARNING;
SIf.Alm[5].strText := 'not used';
SIf.Alm[6].eAlmClass := E_AlmClass.WARNING;
SIf.Alm[6].strText := 'not used';
SIf.Alm[7].eAlmClass := E_AlmClass.INFO;
SIf.Alm[7].strText := 'Data Channel Error Receive Data';

// for tests
CIf.SwCfg.bolSimulation := FALSE;
SIf.Par.udiWaitPick_ms := 15000;]]></ST>
      </Implementation>
    </Method>
    <Method Name="StateMachine" Id="{c802ac33-3cac-4bf2-ad0b-3593f6ab5767}">
      <Declaration><![CDATA[METHOD PROTECTED StateMachine : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.StateMachine();

// main statemachine of base controller
CASE me.StateMachine.intState OF
    cSTA_INITBusy: 
        THIS^.sta_INITBusy();
    cSTA_INIT: 
        THIS^.sta_INIT();
    cSTA_RUNBusy: 
        THIS^.sta_RUNBusy();
    cSTA_RUN: 
        THIS^.sta_RUN();
END_CASE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_ERROR" Id="{bd89ff8b-1c0d-40eb-a1ec-514d6a65e3ac}">
      <Declaration><![CDATA[METHOD PROTECTED sta_ERROR : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateHistory := me.StateMachine.intStateLastCycle; // -> possibillity to go back to state before error
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		;
		
		
		// State Change Condition
		IF (CIf.Job.bolAlmReset = TRUE) THEN
			me.StateMachine.intStateNext := cSTA_ERRORQuit;
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_ERRORQuit" Id="{06630e5b-a1d1-445c-ae3c-6895aea5b0cc}">
      <Declaration><![CDATA[METHOD PROTECTED sta_ERRORQuit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		;
		// evtl. wait time if reset of device is necessary and then decide to go to states or ERROR state
		IF (CIf.Job.bolAlmReset = TRUE) THEN
			CIf.Job.eMe_ActivAlmClass := E_AlmClass.NONE; // reset feedback channel for parent
			CIf.Job.bolAlmReset := FALSE;
		END_IF
		
		// State Change Condition
		IF (CIf.Job.bolAlmReset = FALSE) THEN
			// State Change Condition
			IF ((CIf.Job.eParent_ActivAlmClass=E_AlmClass.EOFF) OR (CIf.Job.eParent_ActivAlmClass=E_AlmClass.OFF)) OR 
				((CIf.Job.eMe_ActivAlmClass=E_AlmClass.EOFF) OR (CIf.Job.eMe_ActivAlmClass=E_AlmClass.OFF)) THEN
				me.StateMachine.intStateNext := cSTA_OFFBusy;
			ELSIF (me.StateMachine.intStateHistory<=cSTA_PON) OR (me.intCmd=cSTA_PON) THEN
				me.StateMachine.intStateNext := cSTA_PONBusy;
			ELSIF(me.intCmd=cCMD_OFF) THEN
				me.StateMachine.intStateNext := cSTA_OFFBusy;
			ELSIF(me.intCmd=cCMD_INIT) THEN
				me.StateMachine.intStateNext := cSTA_INITBusy;
			ELSIF(me.intCmd=cCMD_RUN) THEN
				me.StateMachine.intStateNext := cSTA_INITBusy;
			END_IF
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_INITBusy" Id="{88e407a8-e2cd-48ba-9f7d-666f33f6c29a}">
      <Declaration><![CDATA[METHOD PROTECTED sta_INITBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN

		// Simulation
		IF (CIf.SwCfg.bolSimulation) THEN
			;
		END_IF
		
		// no change action
		IF (pVER_CIF^.Job.intState = cSTA_RETRACT) THEN
			pHOR_CIF^.Job.intACmd := cCMD_RETRACT; // go to home position of horizontal cylinder
		ELSE
			pVER_CIF^.Job.intACmd := cCMD_RETRACT; // cylinder move up, 
			// problem command could not set in entry because if VER in manual moved  during this step to extend....
		END_IF
		
		// State Change Condition
		IF ((me.Alarm.eActivAlmClass=E_AlmClass.EOFF) OR (me.Alarm.eActivAlmClass=E_AlmClass.OFF)) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cSTA_OFF) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF (pVER_CIF^.Job.intState = cSTA_RETRACT) AND (pHOR_CIF^.Job.intState = cSTA_RETRACT) THEN
			me.StateMachine.intStateNext := cSTA_INIT;
		END_IF
	END_IF



//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_INIT" Id="{c042c768-acb4-4558-9397-006a2e69db61}">
      <Declaration><![CDATA[METHOD PROTECTED sta_INIT : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;

	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN

		// Simulation
		IF (CIf.SwCfg.bolSimulation) THEN
			;
		END_IF
		
		// State Change Condition
		// State Change Condition
		IF ((me.Alarm.eActivAlmClass=E_AlmClass.EOFF) OR (me.Alarm.eActivAlmClass=E_AlmClass.OFF)) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cCMD_OFF) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cCMD_RUN) THEN
			me.StateMachine.intStateNext := cSTA_RUNBusy;
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_OFF" Id="{951b3bca-0a64-4af0-b5fe-69f84406a6ce}">
      <Declaration><![CDATA[METHOD PROTECTED sta_OFF : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;

	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN

		// Simulation
		IF (CIf.SwCfg.bolSimulation) THEN
			;
		END_IF
		
		// State Change Condition
		IF ((me.Alarm.eActivAlmClass<>E_AlmClass.EOFF) AND (me.Alarm.eActivAlmClass<>E_AlmClass.OFF)) THEN
			// enable start - no important error active
			IF(me.intCmd=cCMD_INIT) THEN
				me.StateMachine.intStateNext := cSTA_INITBusy;
			ELSIF(me.intCmd=cCMD_RUN) THEN
				me.StateMachine.intStateNext := cSTA_INITBusy;
			END_IF
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_OFFBusy" Id="{a6df9459-5ede-42c8-953e-cda916db252b}">
      <Declaration><![CDATA[METHOD PROTECTED sta_OFFBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
		// no action needed
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN

		// Simulation
		IF (CIf.SwCfg.bolSimulation) THEN
			;
		END_IF
		
		// State Change Condition
		IF (TRUE) THEN
			me.StateMachine.intStateNext := cSTA_OFF;
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="getCtrlSIf" Id="{8030e771-ed10-4085-957e-9f186f9cfcc1}">
      <Declaration><![CDATA[METHOD getCtrlSIf : POINTER TO T_Ctrl_SIf_CYL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getCtrlSIf := ADR(SIf);]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_PON" Id="{cf17ae8c-d7d9-4380-8e30-6241c2206453}">
      <Declaration><![CDATA[METHOD PROTECTED sta_PON : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		;
		
		
		// State Change Condition
		IF(me.intCmd=cCMD_OFF) OR (me.intCmd=cCMD_INIT) OR (me.intCmd=cCMD_RUN) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="Always" Id="{2c870b32-2bc2-4544-82fa-e278fa972b09}">
      <Declaration><![CDATA[METHOD PROTECTED Always : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.Always();

// ---------------------------------------------------------------------------------
// check child me and set parent alarm class
CheckChildAlmClass();

// ----------------------------------------------------------------------------------
// cyclic actions outside of statemachine
IF (me.StateMachine.intState<>cSTA_SINGLE_ANSWER) THEN
	
	// Communication to the Line Controll over Data Channel
	THIS^.DtChnToPlc_Test();
	
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_PONBusy" Id="{bab8c042-e40a-4e22-bc52-ca7f2671848e}">
      <Declaration><![CDATA[METHOD PROTECTED sta_PONBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN
		
		pVER_CIF^.Job.intACmd := cCMD_PON;
		pHOR_CIF^.Job.intACmd := cCMD_PON;
		
		// State Change Condition
		IF (pVER_CIF^.Job.intState = cSTA_PON) AND (pHOR_CIF^.Job.intState = cSTA_PON) THEN
			me.StateMachine.intStateNext := cSTA_PON;
		ELSE
			;
			// timeout power on child not ok -> failure or log entry???
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_RUNBusy" Id="{1f9de034-dff8-477f-b9a8-1c73db7b3b29}">
      <Declaration><![CDATA[METHOD PROTECTED sta_RUNBusy : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		
		stSubSate.intState := cSUB_INITIALIZED; // init sub state machine
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN

		// Simulation
		IF (CIf.SwCfg.bolSimulation) THEN
			;
		END_IF
		
		// no change action
		
		//--------------------------------------------------------------------------------------
		// sub state machine for sequence
		CASE stSubSate.intState OF
			//--------------------------------------------------------------------------------------
			cSUB_INITIALIZED:
					// State Change Condition
					IF(TRUE) THEN
						stSubSate.intStateNext := cSUB_VER_Down;
					END_IF
					
			//--------------------------------------------------------------------------------------
			cSUB_VER_Down:
					pVER_CIF^.Job.intACmd := CYL_CmdState.cCMD_EXTEND; // move to position down
					
					// State Change Condition
					IF (pVER_CIF^.Job.intState=CYL_CmdState.cSTA_EXTEND) AND (pHOR_CIF^.Job.intState=CYL_CmdState.cSTA_RETRACT) THEN // pick position
						stSubSate.intStateNext := cSUB_WAIT_Pick;
					ELSIF (pVER_CIF^.Job.intState=CYL_CmdState.cSTA_EXTEND) THEN // place position
						stSubSate.intStateNext := cSUB_VER_Up;
					END_IF
					
			//--------------------------------------------------------------------------------------
			cSUB_WAIT_Pick:
	
					// State Change Condition
					IF(stSubSate.udiTimeInState_ms>=SIf.Par.udiWaitPick_ms) THEN
						stSubSate.intStateNext := cSUB_VER_Up;
					END_IF
					
			//--------------------------------------------------------------------------------------
			cSUB_VER_Up:
					pVER_CIF^.Job.intACmd := CYL_CmdState.cCMD_RETRACT; // move to position up
					
					// State Change Condition
					IF (pVER_CIF^.Job.intState=CYL_CmdState.cSTA_RETRACT) AND (pHOR_CIF^.Job.intState=CYL_CmdState.cSTA_RETRACT) THEN // pick position
						stSubSate.intStateNext := cSUB_HOR_MoveToPlace;
					ELSIF (pVER_CIF^.Job.intState=CYL_CmdState.cSTA_RETRACT) THEN // place position
						stSubSate.intStateNext := cSUB_HOR_MoveToPick;
					END_IF		
					
			//--------------------------------------------------------------------------------------
			cSUB_HOR_MoveToPlace:
					pHOR_CIF^.Job.intACmd := CYL_CmdState.cCMD_EXTEND; // move to position place
					
					// State Change Condition
					IF(pHOR_CIF^.Job.intState=CYL_CmdState.cSTA_EXTEND) THEN
						stSubSate.intStateNext := cSUB_VER_Down;
					END_IF
					
			//--------------------------------------------------------------------------------------
			cSUB_HOR_MoveToPick:
					pHOR_CIF^.Job.intACmd := CYL_CmdState.cCMD_RETRACT; // move to position pick	
					
					// State Change Condition
					IF(pHOR_CIF^.Job.intState=CYL_CmdState.cSTA_RETRACT) THEN
						stSubSate.intStateNext := cSUB_Finished;
					END_IF
			//--------------------------------------------------------------------------------------
			cSUB_Finished:
					;
					
		END_CASE
		stSubSate.intStateLastCycle := stSubSate.intState;
		CIf.Job.strSubState := THIS^.getCmdState(intCmdState:=stSubSate.intState,bolState:=TRUE);
		IF (stSubSate.intState<>stSubSate.intStateNext) THEN
			stSubSate.intState := stSubSate.intStateNext;  // set next state for next cycle
			stSubSate.udiTimeInState_ms := 0; // reset timer
			// log state change -> open point
		ELSE
			// increment time in state	
			stSubSate.udiTimeInState_ms := stSubSate.udiTimeInState_ms + me.FUBInfo.udiSample_ms;
		END_IF
		
		
		// State Change Condition
		IF ((me.Alarm.eActivAlmClass=E_AlmClass.EOFF) OR (me.Alarm.eActivAlmClass=E_AlmClass.OFF)) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cCMD_OFF) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cCMD_INIT) THEN
			me.StateMachine.intStateNext := cSTA_INITBusy;
		ELSIF (stSubSate.intState=cSUB_Finished) THEN
			me.StateMachine.intStateNext := cSTA_RUN;
		END_IF

	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		SIf.Cur.udiCycleTime_ms := me.StateMachine.udiTimeInState_ms;
		CIf.Job.strSubState := '';
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="sta_RUN" Id="{69ddae28-18e7-4977-bc9f-09a5ed0469e4}">
      <Declaration><![CDATA[METHOD PROTECTED sta_RUN : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// state implemetation
//--------------------------------------------------------------------------------------
// Entry action
	IF (me.StateMachine.intState<>me.StateMachine.intStateLastCycle) THEN
		me.StateMachine.intStateLastCycle := me.StateMachine.intState;
		bolfpCmd := FALSE; // reset flag positive edge of run command
	END_IF


//--------------------------------------------------------------------------------------
// Cyclic action
	IF (me.StateMachine.intState=me.StateMachine.intStateLastCycle) THEN

		// Simulation
		IF (CIf.SwCfg.bolSimulation) THEN
			;
		END_IF
		
		IF (bolfpCmd=FALSE) AND (me.intCmd<>cCMD_RUN) THEN
			bolfpCmd := TRUE;
		END_IF
		
		// State Change Condition
		IF ((me.Alarm.eActivAlmClass=E_AlmClass.EOFF) OR (me.Alarm.eActivAlmClass=E_AlmClass.OFF)) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cCMD_OFF) THEN
			me.StateMachine.intStateNext := cSTA_OFFBusy;
		ELSIF(me.intCmd=cCMD_INIT) THEN
			me.StateMachine.intStateNext := cSTA_INITBusy;
		ELSIF (me.intCmd=cCMD_RUN) AND (bolfpCmd=TRUE) THEN // command RUN and positive flag command was onetime <> RUN
			me.StateMachine.intStateNext := cSTA_RUNBusy;
		END_IF
	END_IF




//--------------------------------------------------------------------------------------
// Exit action
	IF (me.StateMachine.intState<>me.StateMachine.intStateNext) THEN
		;
	END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="CtrlInit" Id="{fa45c37c-8fb5-43fd-999f-238e463548c4}">
      <Declaration><![CDATA[METHOD PROTECTED CtrlInit : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[SUPER^.CtrlInit();

// get child if
pHOR_CIF := p_fbHOR^.getCtrlCIf();
pVER_CIF := p_fbVER^.getCtrlCIf();

// set parent id
pHOR_CIF^.Info.intParentCtrlId := CIf.Info.intCtrlId;
pVER_CIF^.Info.intParentCtrlId := CIf.Info.intCtrlId;

//necessary???
THIS^.CIf.Info.p_aCIf[0] := THIS^.pHOR_CIF;
THIS^.CIf.Info.p_aCIf[1] := THIS^.pVER_CIF;]]></ST>
      </Implementation>
    </Method>
    <Method Name="CheckChildAlmClass" Id="{d45cf562-3263-4dce-9708-d68b105843bd}">
      <Declaration><![CDATA[METHOD CheckChildAlmClass : BOOL
]]></Declaration>
      <Implementation>
        <ST><![CDATA[//------------------------------------------------------------------------------------------------------
// check child alarm classes
IF (pHOR_CIF^.Job.eMe_ActivAlmClass=E_AlmClass.EOFF) OR (pVER_CIF^.Job.eMe_ActivAlmClass=E_AlmClass.EOFF) THEN
	CIf.Job.eMe_ActivAlmClass := E_AlmClass.EOFF;
	me.Alarm.bolfp := TRUE;
ELSE
	IF (me.Alarm.bolfp) THEN
		CIf.Job.eMe_ActivAlmClass := E_AlmClass.NONE; // no reset necessary
		me.Alarm.bolfp := FALSE;
	END_IF
END_IF

//------------------------------------------------------------------------------------------------------
// set alarm class parent of childs
IF (CIf.Job.eParent_ActivAlmClass=E_AlmClass.EOFF) THEN
	pHOR_CIF^.Job.eParent_ActivAlmClass := CIf.Job.eParent_ActivAlmClass;
	pVER_CIF^.Job.eParent_ActivAlmClass := CIf.Job.eParent_ActivAlmClass;
ELSIF(CIf.Job.eMe_ActivAlmClass=E_AlmClass.EOFF) OR (CIf.Job.eMe_ActivAlmClass=E_AlmClass.OFF) THEN
	pHOR_CIF^.Job.eParent_ActivAlmClass := CIf.Job.eMe_ActivAlmClass;
	pVER_CIF^.Job.eParent_ActivAlmClass := CIf.Job.eMe_ActivAlmClass;
ELSE
	// no important alarm for child active
	pHOR_CIF^.Job.eParent_ActivAlmClass := E_AlmClass.NONE;
	pVER_CIF^.Job.eParent_ActivAlmClass := E_AlmClass.NONE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="getCmdState" Id="{d582d51e-d666-468c-a9fc-fe9eb7059b4e}">
      <Declaration><![CDATA[METHOD PROTECTED getCmdState : STRING
VAR_INPUT
	intCmdState: INT;
	bolState: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[getCmdState := SUPER^.getCmdState(intCmdState:=intCmdState,bolState:=bolState); // get global/base defined cmds/states
IF (getCmdState='') THEN // global not defined get local
	// get string cmd/state string information in runtime
	IF (bolState=FALSE) THEN
		// cmd's
		//CASE intCmdState OF 
		//ELSE
				getCmdState := 'not defined';
		//END_CASE;
	ELSE
	
	// state's
		CASE intCmdState OF
			// substates
			cSUB_INITIALIZED: 
				getCmdState := 'cSUB_INITIALIZED';
			cSUB_VER_Up: 
				getCmdState := 'cSUB_VER_Up';
			cSUB_VER_Down: 
				getCmdState := 'cSUB_VER_Down';
			cSUB_WAIT_Pick: 
				getCmdState := 'cSUB_WAIT_Pick';
			cSUB_HOR_MoveToPlace: 
				getCmdState := 'cSUB_HOR_MoveToPlace';
			cSUB_HOR_MoveToPick: 
				getCmdState := 'cSUB_HOR_MoveToPick';
			cSUB_Finished: 
				getCmdState := 'cSUB_Finished';
		END_CASE;
	END_IF
END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="DtChnToPlc_Test" Id="{fc488651-8c69-4492-bec7-b1190b1bdfcb}">
      <Declaration><![CDATA[METHOD DtChnToPlc_Test : BOOL
VAR_INPUT
END_VAR

VAR CONSTANT
	cSTA_SUB_Data_Init: INT := 900;
	cSTA_SUB_Data_WaitForJob: INT := 902;
	cSTA_SUB_Data_CheckData: INT := 904;
	cSTA_SUB_Data_Acknowledge: INT := 906;
	cSTA_SUB_Data_Retry: INT := 908;
	cSTA_SUB_Data_Error: INT := 910;
	
	cDataState_DONE: INT := -1;
	cDataState_Ready : INT := 1;
	cDataState_Retry : INT := -99;
	cDataState_Error : INT := -100;
	
	cMAX_RETRY : INT := 10;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Data Channel to Receive Data from Line Controller
// Startup check States
IF intStatemachine < cSTA_SUB_Data_Init THEN
	intStatemachine := cSTA_SUB_Data_Init;
END_IF

CASE intStatemachine OF
	cSTA_SUB_Data_Init:
		IF CIf.Job.intState > cSTA_PON THEN
			intCountRetry := 0;
			stDataChannelTest_DtChnToPlc.intDataState := cDataState_DONE;
			intStatemachine := cSTA_SUB_Data_WaitForJob;
		END_IF
		
	cSTA_SUB_Data_WaitForJob:
		// New data arrived
		IF stDataChannelTest_DtChnToPlc.intDataState = cDataState_Ready THEN
			bolDataBad := FALSE;
			//dtOldestTag := dtActualTime;
			intIndexOldest := -1;			// -1 = no Oldest free input found
			intStatemachine := cSTA_SUB_Data_CheckData;
		END_IF
	
	cSTA_SUB_Data_CheckData:	
		// Check Data
		//IF stDataChannelTest_DtChnToPlc.stDecision.uliRFID <= 0 THEN
		//		bolDataBad := TRUE;
		//		// Set Alarm: SIf.Alm[3].strText := 'False Job Parameter';
		//		g_fbAlm_Man.setAlarm(CIf.Info.intCtrlId,3,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);					
		//END_IF
 
		// ------------------------------------------------------------------------------------
		// Data Use
		IF bolDataBad = TRUE THEN
			IF intCountRetry >= cMAX_RETRY THEN
				intStatemachine := cSTA_SUB_Data_Error;
			ELSE
				intCountRetry := intCountRetry + 1;
				intStatemachine := cSTA_SUB_Data_Retry;
			END_IF
		ELSE
			// Reset counter because right job
			intCountRetry := 0;

			// do something here
			intLastReadData := stDataChannelTest_DtChnToPlc.dinTest;
			
			intStatemachine := cSTA_SUB_Data_Acknowledge;
			
		END_IF
		
	cSTA_SUB_Data_Acknowledge:	
		// Delete all values
		stDataChannelTest_DtChnToPlc.dinTest := 0;
		// Acknowledge communication
		stDataChannelTest_DtChnToPlc.intDataState := cDataState_DONE;
		intStatemachine := cSTA_SUB_Data_WaitForJob;
		
	cSTA_SUB_Data_Retry:
		stDataChannelTest_DtChnToPlc.intDataState := cDataState_Retry;
		intStatemachine := cSTA_SUB_Data_WaitForJob;

	cSTA_SUB_Data_Error:
		stDataChannelTest_DtChnToPlc.intDataState := cDataState_Error;
		// Set Alarm: SIf.Alm[7].strText := 'Data Channel Error Receive Dat
		g_fbAlm_Man.setAlarm(CIf.Info.intCtrlId,7,0,ADR(SIf.Alm),E_AlmState.RST,CIf.Job.eMe_ActivAlmClass);										
		IF CIf.Job.bolAlmReset = TRUE THEN
			intStatemachine := cSTA_SUB_Data_Init;
		END_IF
			
END_CASE]]></ST>
      </Implementation>
    </Method>
    <ObjectProperties />
  </POU>
</TcPlcObject>