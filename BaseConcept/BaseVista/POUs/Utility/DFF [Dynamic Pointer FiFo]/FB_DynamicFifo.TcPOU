<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4019.2">
  <POU Name="FB_DynamicFifo" Id="{775546da-d62a-4095-8834-9f828c0de9e5}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_DynamicFifo
VAR
	/// Configuration
	_bFreeDataPointer		: BOOL;
	/// Actual Count of the elements in the buffer
	_uinItemCount			: UINT;
	/// Index of the first added element
	_pFirstElement			: POINTER TO T_QueueBase;
	/// Index of the last added element
	_pLastElement			: POINTER TO T_QueueBase;
	/// Limit the queue size  0=NO-LIMIT
	_uinQueueLimit			: UINT := 0;
	/// Behavior when the queue is full FALSE=Do not add any item if full, TRUE= Delete the oldest one and add the new one.
	_bolRingBuffer			: BOOL := FALSE;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Copyright 2017 Cimpress

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License. *)]]></ST>
    </Implementation>
    <Method Name="Pop" Id="{24f665ed-39ef-4d05-a514-5b6e703f4f04}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Pop'}
METHOD Pop : POINTER TO BYTE
VAR
	pItem: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Pop := TC3_MEMMAN.NULL;

IF _uinItemCount > 0 THEN
	pItem := _pFirstElement;
	
	_pFirstElement := _pFirstElement^.pNext;

	IF _pFirstElement <> TC3_MEMMAN.NULL THEN
		_pFirstElement^.pPrevious := TC3_MEMMAN.NULL;
	ELSE
		_pLastElement := TC3_MEMMAN.NULL;
	END_IF

	_uinItemCount := _uinItemCount - 1;

	Pop := pItem^.pData;
	__DELETE(pItem);
END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="Push" Id="{4ba242b0-7aec-4445-afd7-55552d74cb37}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Push'}
METHOD Push : INT
VAR_INPUT
	pData			: POINTER TO BYTE;
END_VAR
VAR
	pDataDel		: POINTER TO BYTE;
	pItem			: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF (_uinQueueLimit = 0) OR (_uinItemCount < _uinQueueLimit) OR ((_uinItemCount = _uinQueueLimit) AND _bolRingBuffer) THEN
	IF (_uinItemCount = _uinQueueLimit) AND _bolRingBuffer THEN
		pDataDel := Pop();
		
		IF pDataDel <> NULL THEN
			__DELETE(pDataDel);
		END_IF
	END_IF	

	AddItem(pData);
	
	Push := UINT_TO_INT(_uinItemCount);
ELSE
	Push := -1;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="AddItem" Id="{529ad006-3ac5-4bbe-bdc7-de0c2849ba14}">
      <Declaration><![CDATA[METHOD PROTECTED AddItem : BOOL
VAR_INPUT
	pData: POINTER TO BYTE;
END_VAR
VAR
	pItem: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pItem := __NEW(T_QueueBase);
pItem^.pData := pData;

IF _uinItemCount = 0 THEN
	_pFirstElement := pItem;
	_pLastElement  := pItem;
	pItem^.pNext := TC3_MEMMAN.NULL;
	pItem^.pPrevious := TC3_MEMMAN.NULL;
ELSE
	_pLastElement^.pNext:= pItem;
	pItem^.pPrevious := _pLastElement;
	pItem^.pNext := TC3_MEMMAN.NULL;
	_pLastElement  := pItem;
END_IF

_uinItemCount := _uinItemCount + 1;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="RingBuffer" Id="{73ecdfe2-b156-4280-8a87-46c390548293}">
      <Declaration><![CDATA[PROPERTY PUBLIC RingBuffer : BOOL]]></Declaration>
      <Set Name="Set" Id="{295596ed-8e6f-47cd-a98e-73bdfb343ae4}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_bolRingBuffer := RingBuffer;]]></ST>
        </Implementation>
      </Set>
      <Get Name="Get" Id="{ee21db1f-dbf3-4c26-b36d-b04ef9c87e3d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[RingBuffer := _bolRingBuffer;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="Peek" Id="{855cb792-2fef-4a6a-97bb-b0c19f8c9d0b}">
      <Declaration><![CDATA[{attribute 'object_name' := 'Peek'}
METHOD Peek : POINTER TO BYTE
]]></Declaration>
      <Implementation>
        <ST><![CDATA[Peek := TC3_MEMMAN.NULL;

IF _uinItemCount > 0 THEN
	Peek := _pFirstElement^.pData;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Property Name="QueueLimit" Id="{d3167dbc-b3fb-4bb1-8b60-b962656f7676}">
      <Declaration><![CDATA[PROPERTY PUBLIC QueueLimit : UINT]]></Declaration>
      <Get Name="Get" Id="{0b11d454-da29-40b5-9b9c-bd4194a4579d}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[QueueLimit := _uinQueueLimit;]]></ST>
        </Implementation>
      </Get>
      <Set Name="Set" Id="{2352dc0c-296a-474f-871e-71385bfca5ed}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[_uinQueueLimit := QueueLimit;]]></ST>
        </Implementation>
      </Set>
    </Property>
    <Property Name="FreeDataPointer" Id="{d8615c9c-cc4e-4f68-8683-8ec365a63b21}">
      <Declaration><![CDATA[PROPERTY FreeDataPointer : BOOL
]]></Declaration>
      <Set Name="Set" Id="{ea585a2d-3864-4fdf-a45a-885e837a622b}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[_bFreeDataPointer := FreeDataPointer;]]></ST>
        </Implementation>
      </Set>
      <Get Name="Get" Id="{ee2e23ab-c3ed-494b-8059-3ca4a2b48bcf}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[FreeDataPointer := _bFreeDataPointer;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="ClearFifo" Id="{de691d59-86b0-441f-ac72-0cd22129d7cb}">
      <Declaration><![CDATA[{attribute 'object_name' := 'ClearFifo'}
METHOD ClearFifo : BOOL
VAR
	pItem: POINTER TO T_QueueBase;
	pNextItem: POINTER TO T_QueueBase;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[pItem := _pFirstElement;
_pFirstElement := NULL;

WHILE pItem <> TC3_MEMMAN.NULL DO
	pNextItem := pItem^.pNext;
	
	IF _bFreeDataPointer THEN
		__DELETE(pItem^.pData);
	END_IF
	
	__DELETE(pItem);
	pItem := pNextItem;
END_WHILE

_pLastElement := NULL;
_uinItemCount := 0;
]]></ST>
      </Implementation>
    </Method>
    <Property Name="ItemCount" Id="{fddbec3a-ffce-4ec3-b578-25480543f3b1}">
      <Declaration><![CDATA[PROPERTY ItemCount : UINT
]]></Declaration>
      <Get Name="Get" Id="{8e08995b-a44b-4e75-94a2-9ed99ad89545}">
        <Declaration><![CDATA[]]></Declaration>
        <Implementation>
          <ST><![CDATA[ItemCount := _uinItemCount;]]></ST>
        </Implementation>
      </Get>
    </Property>
  </POU>
</TcPlcObject>