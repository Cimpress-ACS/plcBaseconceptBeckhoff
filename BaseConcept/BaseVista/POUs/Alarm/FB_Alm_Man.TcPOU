<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4019.2">
  <POU Name="FB_Alm_Man" Id="{1df3d5ca-0fc7-41d6-8e17-a72131e17c96}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Alm_Man
VAR_INPUT
	;
END_VAR
VAR_IN_OUT
	bolCtrlRst: BOOL; // reset all controller
END_VAR

VAR CONSTANT
	/// config alarm handler number of elements of queue
	c_uinMaxAlmElements: UINT := 99;
	/// 0: with history on PLC, auto clr alarms from array if alarm state clear is set
	c_uinModeAlarm: UINT := 1;
	/// machine number 1, at the moment every time set to 1
	c_udiMachineNr: UDINT := 1;
	/// commands from the UI
	cCMD_RESET_ALL: INT := 2;
	/// reset state=RST, reset state CLR
	cCMD_RESET_RST: INT := 4;
	/// reset state CLR -> c_uinModeAlarm = 1, automatic reset will be done on PLC
	cCMD_RESET_CLR: INT := 6;
END_VAR
VAR
	///------------------------------------------------------------
	/// UI interface
	/// alarmarray PLC -> UI
	aAlm: ARRAY[0..c_uinMaxAlmElements] OF T_AlmElement;
	/// command interface for UI -> PLC
	intAlmCmd: INT;
	///------------------------------------------------------------
	fbTimeStamp: FB_TimeStamp; // only one central time for time stamps

	jj: UINT;
	EmptyAlmElement: T_AlmElement;
	intState: INT;
	intTestCtrl: INT := 12;
	intTestState: INT;
	udiTestAlarmNr: UDINT;
	dinTestInfo: DINT;
	aCtrlAlm: ARRAY[1..6] OF T_Ctrl_Alm;
	eActiveAlmClass: E_AlmClass;

	aCntActiveAlmPerClass: ARRAY[0..(E_AlmClass.Max_E_AlmClass-1)] OF UINT; // count off all active alarms per class, idx=0 all active alarms
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Copyright 2017 Cimpress

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License. *)

(* 
Function desription:
The alarm manager sets and clears alarms from the internal array. In the internal array all active 
alarms of a machine are listed. The list has no order. A new entry will be inserted at the next 
free place.

For the easy usage for UI the function block is to be define as global. And then the cmd for reset should be 
set when the button is pressed -> intAlmCmd = cCMD_RESET_ALL
All active alarms are shown in the aAlm array.

History:
Version		Date			Author		Comment
---------------------------------------------------------------------------------------------------
0.00.1		29.04.2013		AVME/KVO	Start history
0.00.2		06.01.2014		AVME/PRE	Implement the counted active messages array
0.00.3		17.02.2014		AVME/KVO	New Property IsInitDone 
										feedback at least one valid time stamp read out 
										-> check at startup before cyclic call of FB's to avoid alarms without valid timestamp
0.00.4		16.05.2014		AVME/DSC	-> Inserted new "setAlarm" Method from Patrik C. (code review)									
										-> BugFix, build sum of all active alarms
0.00.5		07.07.2014		AVME/KVO	-> BugFix Method setAlarm , clr and reset -> also possibel if one entry is zero possibel
0.00.6		07.10.2014		AVME/KVO	-> BugFix alarm reset first statemachine and then the messages	
0.00.7		27.10.2014		AVME/KVO	-> delete GETSYSTEMTIME use FB_TimeStamp
										-> New time properties for easy access of every controller if needed
0.00.8		27.03.2015		CM/RE		Clean alarms array when remove alarm (NULL elements are always at the end now)										
___________________________________________________________________________________________________*)

// call time stamp asynchronous synchronisation
fbTimeStamp.In_strNetId := ''; // use system time of log target system
fbTimeStamp.In_t_CycleTimeRefreshTimeZone := T#0S; // cyclic check time zone settings - not needed using LocalTime_FileTime_BaseWin for log system and local time
fbTimeStamp();

// interface for UI access
CASE intState OF
		
	0: // reset cmd
		intAlmCmd := cCMD_DONE; // Ready for new cmd
		intState := intState + 1;
		
	1: // wait for cmd
		IF (intAlmCmd=cCMD_RESET_ALL) OR (intAlmCmd=cCMD_RESET_CLR) OR  (intAlmCmd=cCMD_RESET_RST) THEN
			intState := intState + 1;
		END_IF
		
	2: // firt reset state machines -> leave Error state
		bolCtrlRst := TRUE;
		intState := intState + 1;
		
	3: // reset alarms / messages
		// reset state=RST, reset state CLR
		THIS^.clearAlarm(((intAlmCmd=cCMD_RESET_ALL) OR (intAlmCmd=cCMD_RESET_RST)), TRUE);
		intState := intState + 1;
		
	4: 
		intState := 0; // done
END_CASE

// build sum of all active alarms
aCntActiveAlmPerClass[0] := 0;
FOR jj:= 1 TO INT_TO_UINT(E_AlmClass.Max_E_AlmClass-1) BY 1 DO
	aCntActiveAlmPerClass[0] := aCntActiveAlmPerClass[0] + aCntActiveAlmPerClass[jj];
END_FOR

//**********************************************************************************************
// Test manual set/reset/clear alarm
//**********************************************************************************************
CASE intTestState OF
	
	0: // wait
		
	1: //CLR
		THIS^.setAlarm(intTestCtrl,udiTestAlarmNr,dinTestInfo,ADR(aCtrlAlm),E_AlmState.CLR,eActiveAlmClass);
		intTestState := 0;
		
	2: // RST
		THIS^.setAlarm(intTestCtrl,udiTestAlarmNr,dinTestInfo,ADR(aCtrlAlm),E_AlmState.RST,eActiveAlmClass);
		intTestState := 0;
		
	3: // SET
		THIS^.setAlarm(intTestCtrl,udiTestAlarmNr,dinTestInfo,ADR(aCtrlAlm),E_AlmState.SET,eActiveAlmClass);
		intTestState := 0;

END_CASE]]></ST>
    </Implementation>
    <Property Name="aCntPerClass" Id="{17110dc3-4d35-4a37-bd6b-2952ae96c5d5}">
      <Declaration><![CDATA[PROPERTY PUBLIC aCntPerClass : ARRAY[0..(E_AlmClass.Max_E_AlmClass-1)] OF UINT]]></Declaration>
      <Get Name="Get" Id="{4d684e1e-963f-4d13-bb51-77b23dd33a98}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[aCntPerClass := aCntActiveAlmPerClass;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="clearAlarm" Id="{4ba0e5bb-ff98-4060-ae97-9ef4f1f4d997}">
      <Declaration><![CDATA[METHOD clearAlarm : BOOL
VAR_INPUT
	///-> clear all entries with state RST
	bolClear_RST: BOOL;
	///-> clear all entries with state CLR
	bolClear_CLR: BOOL;
END_VAR
VAR
	ii: INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[FOR ii:= 0 TO E_AlmClass.Max_E_AlmClass-1 DO
	aCntActiveAlmPerClass[INT_TO_UINT(ii)] := 0; // reset alarm counter
END_FOR

FOR ii:= 0 TO UINT_TO_INT(c_uinMaxAlmElements) DO
	
	IF (aAlm[INT_TO_UINT(ii)].udiNr>0) THEN 
		
		IF (((aAlm[INT_TO_UINT(ii)].intState=E_AlmState.RST) AND (bolClear_RST)) OR
			((aAlm[INT_TO_UINT(ii)].intState=E_AlmState.CLR) AND (bolClear_CLR)))THEN
			
			aAlm[INT_TO_UINT(ii)] := EmptyAlmElement; // clear entry

			// cleanup array (there must not be NULL elements between alarms)
			IF (ii < UINT_TO_INT(c_uinMaxAlmElements)) THEN
				memmove(ADR(aAlm[INT_TO_UINT(ii)]), ADR(aAlm[INT_TO_UINT(ii+1)]), (c_uinMaxAlmElements-INT_TO_UINT(ii)) * SIZEOF(T_Ctrl_Alm));
				ii := ii - 1;
			END_IF
					
		ELSE
			//count alarms per class
			aCntActiveAlmPerClass[INT_TO_UINT(aAlm[INT_TO_UINT(ii)].eClass)] := aCntActiveAlmPerClass[INT_TO_UINT(aAlm[INT_TO_UINT(ii)].eClass)] + 1;
		END_IF
		
	END_IF
END_FOR

]]></ST>
      </Implementation>
    </Method>
    <Property Name="LocalTime_FileTime_BaseWin" Id="{74c9c069-f829-4ef7-8f83-bf2b25485eab}">
      <Declaration><![CDATA[PROPERTY PUBLIC LocalTime_FileTime_BaseWin : T_FileTime]]></Declaration>
      <Get Name="Get" Id="{9e3ad824-c1fe-4c84-843e-204ff419cff4}">
        <Declaration><![CDATA[VAR
	fbToLocal : FB_FileTimeToTzSpecificLocalTime;
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[LocalTime_FileTime_BaseWin := fbTimeStamp.LocalTime_FileTime_BaseWin;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="IsInitDone" Id="{7fbed69f-132f-46cd-9088-e949589753b9}">
      <Declaration><![CDATA[PROPERTY IsInitDone : BOOL]]></Declaration>
      <Get Name="Get" Id="{328f4b2e-2928-43b6-81f0-047d44205740}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[// feedback at least one valid time stamp read out 
//-> check at startup before cyclic call of FB's to avoid alarms without valid timestamp
IsInitDone := fbTimeStamp.Out_bolInitOk;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="UTC_FileTime" Id="{7fca9c03-13dc-4072-9492-be2fc7b1b9b7}">
      <Declaration><![CDATA[PROPERTY PUBLIC UTC_FileTime : T_FileTime
]]></Declaration>
      <Get Name="Get" Id="{1029c51b-ce11-4df3-bb75-cf7cf7b862b7}">
        <Declaration><![CDATA[VAR
	fbGETSYSTEMTIME : GETSYSTEMTIME;
END_VAR]]></Declaration>
        <Implementation>
          <ST><![CDATA[UTC_FileTime := fbTimeStamp.UTC_FileTime;
]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="UTC_TimeStruct" Id="{c7181e8a-e83c-484b-b4d0-e50decf4c9fe}">
      <Declaration><![CDATA[PROPERTY PUBLIC UTC_TimeStruct : TIMESTRUCT]]></Declaration>
      <Get Name="Get" Id="{0acb25b6-30bd-473b-a4fd-3e8acbf86a77}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[UTC_TimeStruct := fbTimeStamp.UTC_TimeStruct;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Property Name="LocalTime_TimeStruct_BaseWin" Id="{ef17aabd-2a81-4dbf-a943-44b889824fdc}">
      <Declaration><![CDATA[PROPERTY PUBLIC LocalTime_TimeStruct_BaseWin : TIMESTRUCT]]></Declaration>
      <Get Name="Get" Id="{f76c6c20-d00e-4548-82d3-55a06f5d912a}">
        <Declaration><![CDATA[VAR

END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[LocalTime_TimeStruct_BaseWin :=fbTimeStamp.LocalTime_TimeStruct_BaseWin;

]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="setAlarm" Id="{f5778ecc-b53c-477d-91b3-54a946399a18}">
      <Declaration><![CDATA[METHOD setAlarm : E_AlmClass
VAR_INPUT
	///from controller 
	intCtrlId: INT;
	///local alarm number
	udiAlarmNr: UDINT;
	///additional information - string???
	dinInfo: DINT;
	///corresponding alarm class
	p_Ctrl_Alm: POINTER TO T_Ctrl_Alm;
	///clear, resetable
	intState: E_AlmState;
END_VAR
VAR_IN_OUT
	eMe_ActivAlmClass: E_AlmClass;
END_VAR
VAR
	InAlmElement: T_AlmElement;
	ii: UINT;
	uinFirstEmptyIdx : UINT;
	e_LocAlmClass: E_AlmClass;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// chack if pointer valid
IF (p_Ctrl_Alm <> Tc3_MemMan.NULL) AND (udiAlarmNr>0) THEN 
	e_LocAlmClass := p_Ctrl_Alm[udiAlarmNr-1].eAlmClass;
ELSE
	// novalid pointer reached!!
   	e_LocAlmClass := E_AlmClass.EOFF; 
END_IF
 
// Copy alarm input data to local alm struct
InAlmElement.udiMachineNr := c_udiMachineNr;
InAlmElement.intCtrlId := intCtrlId;
InAlmElement.udiAlarmNr := udiAlarmNr;
InAlmElement.udiNr := 100000*InAlmElement.udiMachineNr + INT_TO_UDINT(100*InAlmElement.intCtrlId) + InAlmElement.udiAlarmNr;
InAlmElement.dinInfo := dinInfo;
InAlmElement.eClass := e_LocAlmClass;
InAlmElement.intState := intState;
// YYYY-MM-DD-hh:mm:ss.xxx
InAlmElement.strDTEntry := TC2_UTILITIES.SYSTEMTIME_TO_STRING(fbTimeStamp.LocalTime_TimeStruct_BaseWin);
InAlmElement.strText := p_Ctrl_Alm[udiAlarmNr-1].strText;

ii := 0;
uinFirstEmptyIdx := c_uinMaxAlmElements + 1;
//KVO 7.7.2014 AND (aAlm[ii].udiNr <> 0) removed from while because there could be entries above a cleared alarm!
WHILE (ii <= c_uinMaxAlmElements)  AND (InAlmElement.udiNr <> aAlm[ii].udiNr) DO
	IF (aAlm[ii].udiNr = 0) AND (uinFirstEmptyIdx>c_uinMaxAlmElements) THEN // entry first empty index
		uinFirstEmptyIdx := ii;
	END_IF
	ii := ii + 1;              
END_WHILE

// set ii to first empty place (new entry) in array if entry not allready exists in array
IF (uinFirstEmptyIdx<= c_uinMaxAlmElements) AND (InAlmElement.udiNr <> aAlm[ii].udiNr) THEN
	ii := uinFirstEmptyIdx;
END_IF

IF (ii <= c_uinMaxAlmElements) THEN
	
	// Alarm already exist
	IF (aAlm[ii].udiNr = InAlmElement.udiNr) THEN
		CASE InAlmElement.intState OF
			E_AlmState.SET, E_AlmState.RST:
				// refresh state, alarm additional info
				aAlm[ii].dinInfo := InAlmElement.dinInfo;
				aAlm[ii].intState := InAlmElement.intState;
			E_AlmState.CLR:
				IF (aCntActiveAlmPerClass[aAlm[ii].eClass]>0) THEN
					aCntActiveAlmPerClass[aAlm[ii].eClass] := aCntActiveAlmPerClass[aAlm[ii].eClass] - 1; 
				END_IF
				
				IF (c_uinModeAlarm=1) THEN // no history on PLC
					aAlm[ii] := EmptyAlmElement; // clear entry
					
					// cleanup array (there must not be NULL elements between alarms)
					IF (ii < c_uinMaxAlmElements) THEN
						memmove(ADR(aAlm[ii]), ADR(aAlm[ii+1]), (c_uinMaxAlmElements-ii) * SIZEOF(T_Ctrl_Alm));
					END_IF
					
				ELSE
					aAlm[ii].intState := E_AlmState.CLR; // set clear state -> entry in plc history if needed
				END_IF
		END_CASE          
	END_IF
	
	// New Alarm entry
	IF (aAlm[ii].udiNr = 0) THEN
		CASE InAlmElement.intState OF
			E_AlmState.SET, E_AlmState.RST:
				aAlm[ii] := InAlmElement;
				//count alarms per class
				aCntActiveAlmPerClass[aAlm[ii].eClass] := aCntActiveAlmPerClass[aAlm[ii].eClass] + 1; 
	
				IF (InAlmElement.eClass>eMe_ActivAlmClass) THEN 
					eMe_ActivAlmClass := InAlmElement.eClass;
				END_IF
		END_CASE
	END_IF
	
END_IF]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>